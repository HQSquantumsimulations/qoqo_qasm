openqasm    = _{ "OPENQASM" ~ real ~ ";" ~ NEWLINE ~ maincontent }
maincontent = _{ ((q_decl | c_decl | gate_def | include | comment | reset | measurement | gate) ~ NEWLINE | NEWLINE)* }
gate_def    =  { "gate" ~ id ~ parameter_list_def? ~ qubit_list_def ~ gates_definition }
q_decl      =  { "qreg" ~ id ~ "[" ~ integer ~ "]" ~ ";" }
c_decl      =  { "creg" ~ id ~ "[" ~ integer ~ "]" ~ ";" }
gate        =  { id ~ parameter_list? ~ qubit_list ~ ";" }
measurement =  { "measure" ~ argument ~ "->" ~ argument ~ ";" }
include     =  { "include" ~ (!NEWLINE ~ ANY)* }
comment     =  { "//" ~ (!NEWLINE ~ ANY)* }
reset       =  { "reset" ~ argument ~ ";" }

parameter_list     =  { "(" ~ expr ~ ("," ~ expr)* ~ ")" }
qubit_list         =  { argument ~ ("," ~ argument)* }
parameter_list_def =  { "(" ~ id ~ ("," ~ id)* ~ ")" }
argument_list_def  =  { "(" ~ (expr_def | id) ~ ("," ~ (expr_def | id))* ~ ")" }
qubit_list_def     =  { id ~ ("," ~ id)* }
gate_defi          =  { name ~ argument_list_def? ~ qubit_list_def ~ ";" }
gates_definition   =  { NEWLINE? ~ "{" ~ NEWLINE? ~ (gate_defi ~ NEWLINE?)+ ~ "}" }
argument           =  { id ~ "[" ~ integer ~ "]" }
expr               = @{ atom ~ (bin_op ~ atom)* }
expr_def           = @{ (atom | id) ~ (bin_op ~ (id | atom))* }
atom               = _{ unary_minus? ~ primary }
primary            = _{ real | integer | "pi" | "(" ~ expr ~ ")" | unary_op ~ "(" ~ expr ~ ")" }

id          = @{ ASCII_ALPHA_LOWER ~ (ASCII_ALPHANUMERIC | "_")* }
name        = @{ (ASCII_ALPHA_LOWER | ASCII_ALPHA_UPPER) ~ (ASCII_ALPHANUMERIC | "_")* }
real        = @{ "-"? ~ ((ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT*) | (ASCII_DIGIT* ~ "." ~ ASCII_DIGIT+)) ~ (("e" | "E") ~ ("-" | "+")? ~ ASCII_DIGIT+)? }
integer     = @{ ASCII_DIGIT+ }
WHITESPACE  = _{ " " }
bin_op      = _{ "+" | "-" | "*" | "/" | "^" }
unary_minus = _{ "-" }
unary_op    = _{ "sin" | "cos" | "tan" | "exp" | "ln" | "sqrt" }
